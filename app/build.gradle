apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

repositories {
  maven {
    url getArtifactoryUrl()
    credentials {
      username = getArtifactoryUser()
      password = getArtifactoryPassword()
    }
  }
}

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.wellsen.mandiri.whatthehack.android"
    minSdkVersion 23
    targetSdkVersion 28
    multiDexEnabled true
    versionCode 1
    versionName "1.0"
    vectorDrawables.useSupportLibrary = true
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    buildConfigField "String", "ONBOARDING_BASE_URL", "\"" + getOnboardingBaseUrl() + "\""
    buildConfigField "String", "API_GATEWAY_BASE_URL", "\"" + getApiGatewayBaseUrl() + "\""
  }
  buildTypes {
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  dataBinding {
    enabled = true
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'androidx.core:core-ktx:1.0.2'

  implementation 'com.google.firebase:firebase-core:17.0.1'
  implementation 'com.google.firebase:firebase-ml-vision:22.0.0'
  implementation 'com.google.firebase:firebase-auth:18.1.0'
  implementation 'com.google.android.gms:play-services-auth:17.0.0'

  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'com.google.android.material:material:1.0.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  implementation 'androidx.recyclerview:recyclerview:1.0.0'

  implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
  implementation 'androidx.annotation:annotation:1.1.0'

  implementation 'androidx.biometric:biometric:1.0.0-alpha04'

  implementation "org.koin:koin-androidx-scope:$koin_version"
  implementation "org.koin:koin-androidx-viewmodel:$koin_version"
  implementation "org.koin:koin-androidx-ext:$koin_version"

  implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
  implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

  implementation "com.github.bumptech.glide:glide:$glide_version"
  implementation 'androidx.legacy:legacy-support-v4:1.0.0'
  kapt "com.github.bumptech.glide:compiler:$glide_version"

  implementation 'com.karumi:dexter:5.0.0'
  implementation 'id.zelory:compressor:2.1.0'
  implementation 'com.github.gcacace:signature-pad:1.2.1'
  implementation 'com.jakewharton.timber:timber:4.7.1'
  implementation 'com.jakewharton.threetenabp:threetenabp:1.2.1'
  implementation(group: 'ss.com.bannerslider', name: 'Banner-Slider', version: '2.1.2', ext: 'aar')
  implementation 'me.dm7.barcodescanner:zxing:1.9.13'

  implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.10'

  testImplementation 'junit:junit:4.12'
  testImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test:runner:1.2.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

/*
 * *
 *  * Created by Wellsen on 7/20/19 4:08 PM
 *  * for Mandiri What The Hack Hackathon
 *  * Copyright (c) 2019 . All rights reserved.
 *  * Last modified 7/20/19 1:53 PM
 *
 */

// Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'

// If you're using the Android Databinding library,
// you may want to increase the number of errors that javac will print.
// When Dagger prints an status, databinding compilation will halt and sometimes print more than 100 errors,
// which is the default amount for javac. For more information, see Issue 306.
gradle.projectsEvaluated {
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xmaxerrs" << "500" // or whatever number you want
  }
}

def getOnboardingBaseUrl() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty("ONBOARDING_BASE_URL")
}

def getApiGatewayBaseUrl() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty("API_GATEWAY_BASE_URL")
}

def getArtifactoryUrl() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty("ARTIFACTORY_URL")
}

def getArtifactoryUser() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty("ARTIFACTORY_USER")
}

def getArtifactoryPassword() {
  Properties properties = new Properties()
  properties.load(project.rootProject.file('local.properties').newDataInputStream())
  return properties.getProperty("ARTIFACTORY_PASSWORD")
}
